report: "report/workflow.rst"


include: "rules/common.smk"


##### 1. Extract information from config file
(
    DF_PROJECTS,
    DF_SAMPLES,
    PROKKA_DB_TABLE,
    PROKKA_DB_MAP,
    PEP_PROJECTS,
) = extract_project_information(config, project_variable="bgc_projects") # specify project type

# generate centralized sample datasets
bgcflow_util_dir = Path("data/interim/bgcflow_utils")
bgcflow_util_dir.mkdir(parents=True, exist_ok=True)
DF_SAMPLES.to_csv(bgcflow_util_dir / "samples.csv", index=False)

##### 2. Generate wildcard constants #####
PROJECT_IDS = list(DF_PROJECTS.name.unique())
STRAINS = DF_SAMPLES.genome_id.to_list()
CUSTOM = DF_SAMPLES[DF_SAMPLES.source.eq("custom")].genome_id.to_list()
NCBI = DF_SAMPLES[DF_SAMPLES.source.eq("ncbi")].genome_id.to_list()
PATRIC = DF_SAMPLES[DF_SAMPLES.source.eq("patric")].genome_id.to_list()
SAMPLE_PATHS = list(DF_PROJECTS.samples.unique())
GTDB_PATHS = [
    str(PEP_PROJECTS[k].config["gtdb-tax"])
    for k in PEP_PROJECTS.keys()
    if "gtdb-tax" in PEP_PROJECTS[k].config.keys()
]
PROKKA_GBFF = list(PROKKA_DB_TABLE.keys())


##### 3. Wildcard constraints #####
wildcard_constraints:
    strains="|".join(STRAINS),
    ncbi="|".join(NCBI),
    custom="|".join(CUSTOM),
    patric="|".join(PATRIC),
    name="|".join(PROJECT_IDS),
    prokka_db="|".join(PROKKA_GBFF),


##### 4. Generate user-defined local resources
custom_resource_dir()

##### Target rules #####
rule all:
    input:
        expand("data/processed/{name}/tables/df_gtdb_meta.csv", name=PROJECT_IDS),
        expand("data/processed/{name}/bigscape/result_as{version}/index.html", name=PROJECT_IDS, version=dependency_version["antismash"]),
        expand("data/processed/{name}/bigslice/cluster_as_{version}/", name=PROJECT_IDS, version=dependency_version["antismash"]),
        expand("data/processed/{name}/bigslice/query_as_{version}/", name=PROJECT_IDS, version=dependency_version["antismash"]),

include: "rules/gtdb.smk"
include: "rules/bgc_selection.smk" # need to be better
include: "rules/bigscape.smk"
include: "rules/bgc_analytics.smk"
include: "rules/antismash.smk"
include: "rules/bigslice.smk"
